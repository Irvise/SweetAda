
ENTRY(_start)

SECTIONS
{
        /**********************************************************************
         *                                                                    *
         **********************************************************************/
        /*
         * Read-only sections, merged into text segment.
         * Assumes the stand Makefile sets the start address via -Ttext.
         */
        .text :
        {
                _ftext = . ;
                *(.text)
                *(.text.*)
                *(.gnu.warning)
        } =0
        _etext = .;
        PROVIDE (etext = .);
        /**********************************************************************
         *                                                                    *
         **********************************************************************/
        .rodata :
        {
                *(.rodata)
                *(.rodata.*)
        }
        /**********************************************************************
         *                                                                    *
         **********************************************************************/
        .data :
        {
                _fdata = . ;
                *(.data)
                *(.data.*)
                CONSTRUCTORS
        }
        _gp = ALIGN(16);
        .lit8 : { *(.lit8) }
        .lit4 : { *(.lit4) }
        /**********************************************************************
         *                                                                    *
         **********************************************************************/
        .sdata :
        {
                *(.sdata)
        }
        _edata  =  .;
        PROVIDE (edata = .);
        /**********************************************************************
         *                                                                    *
         **********************************************************************/
        __bss_start = .;
        _fbss = .;
        .sbss :
        {
                *(.sbss)
                *(.scommon)
        }
        .bss :
        {
                *(.bss)
                *(COMMON)
        }
        _end = . ;
        PROVIDE (end = .);
}

