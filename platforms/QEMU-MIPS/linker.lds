
ENTRY(_start)

MEMORY
{
        RAM : ORIGIN = 0xA0000000, LENGTH = 16M
        ROM : ORIGIN = 0xBFC00000, LENGTH = 1M
}

SECTIONS
{
        /**********************************************************************
         *                                                                    *
         **********************************************************************/
        .text :
        {
                _stext = . ;
                *(.vectors)
                *(.text)
                *(.text.*)
                *(.rodata)
                *(.rodata.*)
                *(.gnu.warning)
        } > ROM
        _etext = .;
        PROVIDE (etext = .);
        /**********************************************************************
         *                                                                    *
         **********************************************************************/
        .data : AT(_etext)
        {
                _sdata = . ;
                *(.data)
                *(.data.*)
                *(.sdata)
                . = ALIGN(0x10);
                _edata = .;
        } > RAM
        _gp = ALIGN(16);
        .lit8 : { *(.lit8) }
        .lit4 : { *(.lit4) }
        /**********************************************************************
         *                                                                    *
         **********************************************************************/
        .bss ADDR(.data) + SIZEOF(.data) (NOLOAD) :
        {
                _sbss = .;
                *(.bss)
                *(.bss.*)
                *(COMMON)
                *(.sbss)
                *(.sbss.*)
                *(.scommon)
                *(.dynbss)
                _ebss = .;
        } > RAM
        _end = . ;
        PROVIDE (end = .);
        /**********************************************************************
         *                                                                    *
         **********************************************************************/
        /DISCARD/ :
        {
                *(.comment)
                /* *(.eh_frame) */
                *(.rel.dyn)
                *(.MIPS.abiflags)
        }
}

