
ENTRY(_start)

MEMORY
{
        /* ROM-based system */
        /* ROMBM : ORIGIN = 0xFFD00000, LENGTH = 0x1000 */
        ROMBM : ORIGIN = 0x00000000, LENGTH = 0x1000
        ROM   : ORIGIN = 0x70001000, LENGTH = 1M - 0x1000
        RAM   : ORIGIN = 0x00001000, LENGTH = 64M - 0x1000
}

SECTIONS
{
        /**********************************************************************
         *                                                                    *
         **********************************************************************/
        .reset : AT(0x70000000)
        {
                *(.startup)
                . = 0x1000;
        } > ROMBM
        /**********************************************************************
         *                                                                    *
         **********************************************************************/
        .text :
        {
                _stext = .;
                *(.text)
                *(.text.*)
                *(.rodata)
                *(.rodata.*)
                . = ALIGN(0x10);
                _etext = .;
        } > ROM
        /**********************************************************************
         *                                                                    *
         **********************************************************************/
        .data : AT(_etext)
        {
                _sdata = .;
                *(.data)
                *(.data.*)
                *(.sdata.*)
                _sdata2 = .;
                *(.sdata2)
                . = ALIGN(0x10);
                _edata = .;
        } > RAM
        /**********************************************************************
         *                                                                    *
         **********************************************************************/
        .bss ADDR(.data) + SIZEOF(.data) (NOLOAD) :
        {
                _sbss = .;
                *(.bss)
                *(.bss.*)
                *(COMMON)
                *(.sbss)
                *(.sbss.*)
                *(.scommon)
                *(.dynbss)
                . = ALIGN(0x10);
                _ebss = .;
        } > RAM
        /**********************************************************************
         *                                                                    *
         **********************************************************************/
        /DISCARD/ :
        {
                *(.comment)
                /* *(.eh_frame) */
                /* *(.got) */
                /* *(.got1) */
                /* *(.got.plt) */
        }
}

